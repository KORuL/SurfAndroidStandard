apply from: '../config.gradle'
apply plugin: 'maven'

// Функция, конфигурирующая зависимости android-standard для текущего модуля,
// контекст которого передается параметром.

gradle.ext.configAndroidStandardDependencies = { context, modules = [] ->
    // добавление зависимостей android-standard
    context.dependencies {
        modules.each {
            compileOnly context.project(":$it")
            testImplementation context.project(":$it")
            androidTestImplementation context.project(":$it")
        }
    }
    // определние функции для деплоя текущего модуля в artifactory
    context.afterEvaluate {
        uploadArchives {
            def moduleName = project.name
            def projectVersion = defaultVersionName
            def moduleVersion = moduleVersionName

            //функция для деплоя артефакта с определенной версией
            gradle.ext.uploadArtifact = { artifactVersion ->
                repositories.mavenDeployer {
                    repository(url: "https://artifactory.surfstudio.ru/artifactory/libs-release-local") {
                        authentication(
                                userName: System.getenv('surf_maven_username'),
                                password: System.getenv('surf_maven_password'))
                    }

                    def artifactGroupId = 'ru.surfstudio.android'

                    pom.groupId = artifactGroupId
                    pom.artifactId = project.name
                    pom.version = artifactVersion

                    pom.project {
                        dependencies {
                            modules.each {
                                dependency {
                                    groupId artifactGroupId
                                    artifactId moduleName
                                    version artifactVersion
                                    scope "compile"
                                }
                            }
                        }
                    } // pom
                } // repositories.mavenDeployer
            } //gradle.ext.uploadArtifact

            // деплой артефакта с версией проекта
             gradle.ext.uploadArtifact("$projectVersion")

            // деплой артефакта с remote-версией
            // Нужно для выкладки артефакта с кастомной версией в Artifactory и дальнейшего релиза в JCenter
            def shouldUploadArtifact = "$moduleVersion" != "$projectVersion" && !"$projectVersion".contains("SNAPSHOT")
            if (shouldUploadArtifact) {
                gradle.ext.uploadArtifact("$moduleVersion")
                println("Artifact uploaded $moduleName:$moduleVersion")
            }
        } // uploadArchives
    }
}