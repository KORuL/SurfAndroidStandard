apply from: '../config.gradle'
apply plugin: 'maven'

// Функция, конфигурирующая зависимости android-standard для текущего модуля,
// контекст которого передается параметром.

gradle.ext.configAndroidStandardDependencies = { context, modules = [] ->
    // добавление зависимостей android-standard
    context.dependencies {
        modules.each {
            compileOnly context.project(":$it")
            testImplementation context.project(":$it")
            androidTestImplementation context.project(":$it")
        }
    }
    // определние функции для деплоя текущего модуля в artifactory
    context.afterEvaluate {
        uploadArchives {

            //функция для деплоя артефакта с определенной версией
            gradle.ext.uploadArtifact = { repositoryName, artifactVersion ->
                repositories.mavenDeployer {
                    repository(url: "https://artifactory.surfstudio.ru/artifactory/$repositoryName") {
                        authentication(
                                userName: System.getenv('surf_maven_username'),
                                password: System.getenv('surf_maven_password'))
                    }

                    def artifactGroupId = 'ru.surfstudio.android'

                    pom.groupId = artifactGroupId
                    pom.artifactId = project.name
                    pom.version = artifactVersion

                    pom.project {
                        dependencies {
                            modules.each {
                                def moduleName = "$it"
                                dependency {
                                    groupId artifactGroupId
                                    artifactId moduleName
                                    version artifactVersion
                                    scope "compile"
                                }
                            }
                        }
                    } // pom
                } // repositories.mavenDeployer
            } //gradle.ext.uploadArtifact

            //деплой артефакта с версией модуля
            gradle.ext.uploadArtifact("libs-release-local", "$moduleVersionName")

            // деплой артефакта с remote-версией. Нужно для выкладки артефакта с кастомной версией в Artefactory
            // и дальнейшего релиза в JCenter
            // TODO подумать над тем, когда нужно сделать этот флаг активным, например только для веток без подписи snapshot
            def shouldUploadArtifact = "$moduleVersionNameRemote" != ""
            if (shouldUploadArtifact) {
                gradle.ext.uploadArtifact("artifacts-release-local", "$moduleVersionNameRemote")
            }
        } // uploadArchives
    }
}