apply from: '../android-standard/androidStandardConfig.gradle'
import groovy.json.JsonSlurper

// Скрипт для включения модулей android-standard из локального репозитория в settings.gradle

if (gradle.ext.androidStandard.debugEnabled) {
    try {
        def androidStandardDebugDir = gradle.ext.androidStandard.debugDir
        def components = new JsonSlurper().parse(new File("$androidStandardDebugDir/buildSrc/components.json"), 'utf-8')
        assert components instanceof List
        includeBuild("$androidStandardDebugDir") {
            dependencySubstitution {
                components.each { component ->
                    if (component.libs != null && !component.libs.isEmpty()) {
                        component.libs.each { library ->
                            def libraryName = library.name
                            logger.debug("component ${component.dir} / library name: $libraryName, dir: ${library.dir}, artifact name: ${library.artifact_name}")
                            def aarLibraryName = "ru.surfstudio.android:"
                            def artifactName = library.artifact_name
                            if (artifactName != null && !artifactName.toString().isEmpty()) {
                                aarLibraryName += artifactName
                                logger.debug("replacing aar library $aarLibraryName by :$libraryName")
                                substitute module(aarLibraryName) with project(":$libraryName")
                            } else {
                                logger.error("incorrect aar library name: \"$aarLibraryName\"")
                            }
                        }
                    }
                }
            }
        }
    } catch (FileNotFoundException ignored) {
        logger.error("file buildSrc/components.json  in local android standard folder  not found")
    }
}