import ru.surfstudio.android.build.InitializatorKt

apply from: '../../buildSrc/config.gradle'
apply plugin: 'maven'

// Функция, конфигурирующая зависимости android-standard для текущего модуля,
// контекст которого передается параметром.

ext.configDeploy = { context ->
    // определние функции для деплоя текущего модуля в artifactory
    context.afterEvaluate {
        uploadArchives {
            repositories.mavenDeployer {
                repository(url: 'https://artifactory.surfstudio.ru/artifactory/libs-release-local') {
                    authentication(
                            userName: System.getenv('surf_maven_username'),
                            password: System.getenv('surf_maven_password')
                    )
                }

                def artifactGroupId = 'ru.surfstudio.android'
                def artifactName = InitializatorKt.getArtifactNameByLibName(project.name)
                def artifactVersion = InitializatorKt.getModuleVersionName(project)

                pom.groupId = artifactGroupId
                pom.artifactId = artifactName
                pom.version = artifactVersion

                pom.project {
                    dependencies {
                        InitializatorKt.getDependsArtifactNamesByLibName(project.name).each {
                            def depArtifactName = it
                            def depArtifactVersion = InitializatorKt.getModuleVersionName(project.rootProject.project(it))
                            dependency {
                                groupId artifactGroupId
                                artifactId depArtifactName
                                version depArtifactVersion
                                scope "compile"
                            }
                        }
                    }
                }
                //This code delete "implementation project("...") dependencies from pom file"
                pom.whenConfigured {
                    p ->
                        p.dependencies = p.dependencies.findAll {
                            dep -> dep.groupId != "android-standard"
                        }
                }
            } // repositories.mavenDeployer
        } // uploadArchives
    }
}