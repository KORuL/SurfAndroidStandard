import ru.surfstudio.android.build.Components
import ru.surfstudio.android.build.GradleProperties
import ru.surfstudio.android.build.artifactory.Artifactory
import ru.surfstudio.android.build.artifactory.ArtifactoryConfig
import ru.surfstudio.android.build.Initializator
import ru.surfstudio.android.build.PublishType
import ru.surfstudio.android.build.PublishUtil
import ru.surfstudio.android.build.publish.PublishConfig

apply from: "$rootDir/buildSrc/config.gradle"
apply plugin: 'maven-publish'
apply plugin: 'signing'

String currentBuildDirectory = gradle.startParameter.currentDir.path
Initializator.init(currentBuildDirectory)

def trueValue = "true"

// Function for configuration of dependencies of current android-standard module.
// Context of current module is passed in parameters.

ext["signing.keyId"] = System.getenv("surf_maven_sign_key_id")
ext["signing.password"] = System.getenv("surf_maven_sign_password")
ext["signing.secretKeyRingFile"] = System.getenv("surf_maven_sign_key_ring_file")

ext.configureDeploy = { context ->
    // define function for deploy of current module to artifactory or maven central
    context.afterEvaluate {

        // add java source to artifacts
        task sourcesJar(type: Jar) {
            from android.sourceSets.main.java.srcDirs
        }

        def artifactName = Components.getArtifactName(project.name)
        def artifactGroupId = ArtifactoryConfig.ANDROID_STANDARD_GROUP_ID
        def artifactVersion = Components.getModuleVersion(project.name)
        def artifactDescription = Components.getArtifactDescription(project.name)
        def artifactUrl = Components.getArtifactUrl(project.name)
        def publishData = PublishUtil.initPublishData()

        if (!artifactName.trim()) {
            throw new GradleException("artifactName for ${project.name} can not be blank for deploy")
        }

        signing {
            sign publishing.publications
        }

        publishing {
            publications {
                mavenRelease(MavenPublication) {
                    // check publishType
                    if (project.hasProperty(GradleProperties.PUBLISH_TYPE)) {
                        def publishTypeProperty = project.property(GradleProperties.PUBLISH_TYPE)
                        def publishType = PublishType.getById(publishTypeProperty)
                        if (publishType == PublishType.UNKNOWN) {
                            throw new GradleException("Illegal publishType set: $publishTypeProperty")
                        }
                        publishData = PublishUtil.getPublishData(publishType)
                    }

                    // If -PonlyUnstable=true deploy only unstable artifacts
                    if (project.hasProperty(GradleProperties.DEPLOY_ONLY_UNSTABLE_COMPONENTS)) {
                        if (project.property(GradleProperties.DEPLOY_ONLY_UNSTABLE_COMPONENTS) == trueValue
                                && Components.getComponentStability(project.name)
                        ) return
                    }

                    // If -PdeployOnlyIfNotExist=true deploy only artifacts that don't exist in artifactory
                    if (project.hasProperty(GradleProperties.DEPLOY_ONLY_IF_NOT_EXIST)) {
                        if (project.property(GradleProperties.DEPLOY_ONLY_IF_NOT_EXIST) == trueValue
                                && publishType == PublishType.ARTIFACTORY
                                && Artifactory.isLibraryAlreadyDeployed(project.name)
                        ) return
                    }

                    groupId = artifactGroupId
                    artifactId = artifactName
                    version = artifactVersion

                    from components.release

                    pom {
                        name = artifactName
                        description = artifactDescription
                        url = artifactUrl

                        organization {
                            name = PublishConfig.DEVELOPER_ORGANISATION
                            url = PublishConfig.DEVELOPER_ORGANISATION_URL
                        }

                        issueManagement {
                            system = PublishConfig.ISSUE_MANAGEMENT_SYSTEM
                            url = PublishConfig.ISSUE_MANAGEMENT_URL
                        }

                        licenses {
                            license {
                                name = PublishConfig.LICENSE_NAME
                                url = PublishConfig.LICENSE_URL
                            }
                        }

                        scm {
                            connection = PublishConfig.SCM_CONNECTION
                            developerConnection = PublishConfig.SCM_DEVELOPER_CONNECTION
                            url = PublishConfig.SCM_URL
                        }

                        developers {
                            developer {
                                name = PublishConfig.DEVELOPER_NAME
                                email = PublishConfig.DEVELOPER_MAIL
                            }
                        }
                    }   // pom

                    artifact sourcesJar {
                        classifier = "sources"
                    }
                }   // mavenRelease
            }   // publications

            repositories {
                maven {
                    name = PublishConfig.REPOSITORY_NAME
                    url = publishData.url
                    credentials {
                        username System.getenv(publishData.userNameEnvName)
                        password System.getenv(publishData.passwordEnvName)
                    }
                }   // maven
            } // repositories
        } // publishing

        tasks.withType(PublishToMavenRepository) {
            doFirst {
                println("Publishing ${publication.groupId}:${publication.artifactId}:${publication.version} to ${repository.url}")
            }
        }
    }
}